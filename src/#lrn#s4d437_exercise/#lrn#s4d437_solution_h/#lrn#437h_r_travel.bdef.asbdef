managed implementation in class /lrn/bp_437h_r_travel unique;
strict ( 2 );

with draft;

extensible
{
  with additional save;
  with validations on save;
  with determinations on modify;
  with determinations on save;
}

define behavior for /LRN/437h_R_Travel alias Travel
persistent table /lrn/437h_travel ##UNMAPPED_FIELD
draft table /lrn/437h_trvl_d
lock master total etag ChangedAt
authorization master ( instance )
etag master LocChangedAt
early numbering
extensible
with additional save
{
  create;
  update ( features : instance );
  delete;

  action ( features : instance ) cancel_travel;

  event TravelCreated parameter /LRN/437h_A_Event;

  association _TravelItem { create; with draft; }

  field ( readonly )
  AgencyId,
  TravelId;

  field ( readonly )
  Status,
  Duration,
  ChangedAt,
  ChangedBy,
  LocChangedAt;

  field ( mandatory )
  Description,
  EndDate;

  field ( features : instance )
  CustomerId,
  BeginDate;

  validation validateDescription on save
  { create;
    field Description;
  }

  validation validateCustomer on save
  { create;
    field CustomerId;
  }

  validation validateBeginDate on save
  { create;
    field BeginDate;
  }

  validation validateEndDate on save
  { create;
    field EndDate;
  }

  validation validateDateSequence on save
  { create;
    field BeginDate, EndDate;
  }

  determination determineDuration on save
  { field BeginDate, EndDate;
  }

  determine action checkCustomer
  {
    validation validateCustomer;
  }

  determine action adjustDuration
  {
    validation validateBeginDate;
    validation validateEnddate;
    validation validateDateSequence;

    determination determineDuration;
  }

  determine action adjustTravelDates
  {
    determination Item~determineTravelDates;
  }

  side effects
  {
    determine action adjustDuration
         executed on field BeginDate,
                     field EndDate
             affects field Duration,
                     messages
                     ;
  }

  draft action Edit;
  draft action Activate optimized;
  draft action Discard;
  draft action Resume;
  draft determine action Prepare
  extensible
  {
    validation validateDescription;
    validation validateCustomer;
    validation validateBeginDate;
    validation validateEndDate;
    validation validateDateSequence;
    validation Item~ValidateFlightDate;
  }

  mapping for /lrn/437h_travel corresponding ##unextensible_mapping
    {
      AgencyID     = agency_ID;
      TravelID     = travel_id;
      CustomerID   = customer_id;
      BeginDate    = begin_date;
      EndDate      = end_date;
      ChangedAt    = changed_at;
      ChangedBy    = changed_by;
      LocChangedAt = loc_changed_at;
    }

}

define behavior for /LRN/437h_R_TravelItem alias Item
with unmanaged save
draft table /lrn/437h_trit_d
authorization dependent by _Travel
lock dependent by _Travel
etag master LocChangedAt
extensible
{

  update;
  delete;

  association _Travel { with draft; }

  field ( readonly, numbering : managed )
  ItemUuid;

  field ( readonly )
  AgencyId,
  TravelId,
  BookingId;

  validation validateFlightDate on save
  { create;
    field FlightDate;
  }

  determination determineTravelDates on save
  {
    field FlightDate;
  }

  mapping for /lrn/437_s_tritem control /lrn/437_s_tritemx corresponding ##unextensible_mapping
    {
      ItemUuid           = item_uuid;
      AgencyId           = agency_id;
      TravelId           = travel_id;
      CarrierId          = carrier_id;
      ConnectionId       = connection_id;
      FlightDate         = flight_date;
      BookingId          = booking_id;
      PassengerFirstName = passenger_first_name;
      PassengerLastName  = passenger_last_name;
      ChangedAt          = changed_at;
      ChangedBy          = changed_by;
      LocChangedAt       = loc_changed_at;
    }

}