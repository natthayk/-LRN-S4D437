managed implementation in class /lrn/bp_437f_r_travel unique;
strict ( 2 );

with draft;

define behavior for /LRN/437f_R_Travel alias Travel
persistent table /lrn/437f_travel ##UNMAPPED_FIELD
draft table /lrn/437f_trvl_d
lock master total etag ChangedAt
authorization master ( instance )
etag master LocChangedAt
early numbering
{
  create;
  update ( features : instance );
  delete;

  action ( features : instance ) cancel_travel;

  association _TravelItem { create; with draft; }

  field ( readonly )
  AgencyId,
  TravelId;

  field ( readonly )
  Status,
  Duration,
  ChangedAt,
  ChangedBy,
  LocChangedAt;

  field ( mandatory )
  Description,
  EndDate;

  field ( features : instance )
  CustomerId,
  BeginDate;

  validation validateDescription on save
  { create;
    field Description;
  }

  validation validateCustomer on save
  { create;
    field CustomerId;
  }

  validation validateBeginDate on save
  { create;
    field BeginDate;
  }

  validation validateEndDate on save
  { create;
    field EndDate;
  }

  validation validateDateSequence on save
  { create;
    field BeginDate, EndDate;
  }

  determination determineDuration on save
  { field BeginDate, EndDate;
  }

  determine action checkCustomer
  {
    validation validateCustomer;
  }

  determine action adjustDuration
  {
    validation validateBeginDate;
    validation validateEnddate;
    validation validateDateSequence;

    determination determineDuration;
  }

  determine action adjustTravelDates
  {
    //    validation Item~validateFlightDate;
    determination Item~determineTravelDates;
  }

  side effects
  {
    determine action adjustDuration
         executed on field BeginDate,
                     field EndDate
             affects field Duration,
                     messages
                     ;
  }

  draft action Edit;
  draft action Activate optimized;
  draft action Discard;
  draft action Resume;
  draft determine action Prepare
  {
    validation validateDescription;
    validation validateCustomer;
    validation validateBeginDate;
    validation validateEndDate;
    validation validateDateSequence;
    validation Item~ValidateFlightDate;
    //    determination Item~determineTravelDates;
  }

  mapping for /lrn/437f_travel corresponding
    {
      AgencyID     = agency_ID;
      TravelID     = travel_id;
      CustomerID   = customer_id;
      BeginDate    = begin_date;
      EndDate      = end_date;
      ChangedAt    = changed_at;
      ChangedBy    = changed_by;
      LocChangedAt = loc_changed_at;
    }

}

define behavior for /LRN/437f_R_TravelItem alias Item

//persistent table /lrn/437f_tritem
//with additional save

with unmanaged save

draft table /lrn/437f_trit_d

authorization dependent by _Travel
lock dependent by _Travel
etag master LocChangedAt
{

  update;
  delete;

  association _Travel { with draft; }

  field ( readonly, numbering : managed )
  ItemUuid;

  field ( readonly )
  AgencyId,
  TravelId,
  BookingId;

  validation validateFlightDate on save
  { create;
    field FlightDate;
  }

  determination determineTravelDates on save
  {
    field FlightDate;
  }

  //  mapping for /lrn/437f_tritem corresponding
  //    {
  //      ItemUuid           = item_uuid;
  //      AgencyId           = agency_id;
  //      TravelId           = travel_id;
  //      CarrierId          = carrier_id;
  //      ConnectionId       = connection_id;
  //      FlightDate         = flight_date;
  //      BookingId          = booking_id;
  //      PassengerFirstName = passenger_first_name;
  //      PassengerLastName  = passenger_last_name;
  //      ChangedAt          = changed_at;
  //      ChangedBy          = changed_by;
  //      LocChangedAt       = loc_changed_at;
  //    }

  mapping for /lrn/437_s_tritem control /lrn/437_s_tritemx corresponding
    {
      ItemUuid           = item_uuid;
      AgencyId           = agency_id;
      TravelId           = travel_id;
      CarrierId          = carrier_id;
      ConnectionId       = connection_id;
      FlightDate         = flight_date;
      BookingId          = booking_id;
      PassengerFirstName = passenger_first_name;
      PassengerLastName  = passenger_last_name;
      ChangedAt          = changed_at;
      ChangedBy          = changed_by;
      LocChangedAt       = loc_changed_at;
    }

}